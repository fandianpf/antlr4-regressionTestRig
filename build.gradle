/*
 * [The "BSD license"]
 *  Copyright (c) 2014 FandianPF (Stephen Gaito)
 *  All rights reserved.
 *
 *  Redistribution and use in source and binary forms, with or without
 *  modification, are permitted provided that the following conditions
 *  are met:
 *
 *  1. Redistributions of source code must retain the above copyright
 *     notice, this list of conditions and the following disclaimer.
 *  2. Redistributions in binary form must reproduce the above copyright
 *     notice, this list of conditions and the following disclaimer in the
 *     documentation and/or other materials provided with the distribution.
 *  3. The name of the author may not be used to endorse or promote products
 *     derived from this software without specific prior written permission.
 *
 *  THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 *  IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 *  OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 *  IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 *  INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 *  NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 *  DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 *  THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 *  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 *  THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

// Gradle build configuration for the antlr4-gradle-regressionTestPlugin
//
// This gradle build configuraiton publishes the resulting plugin jar
// into an ivy repository on local disk.  This means that you need to 
// place something like the following into the gradle.properties file
// located in your personal .gradle directory:
//
// fandianpfPublishRepo=<<Path to your local repository>>
// fandianpfDownloadRepo=http://fandianpf.github.io/ivyRepo
//

// Add the antlr4-gradle-plugin to the buildscript startup dependencies.
buildscript {
  repositories {
    maven {
      name 'JFrog OSS snapshot repo'
      url 'https://oss.jfrog.org/oss-snapshot-local/'
    }
    mavenCentral()
  }

  dependencies {
    classpath 'me.champeau.gradle:antlr4-gradle-plugin:0.1-SNAPSHOT'
  }
}

apply plugin: 'antlr4'
apply plugin: 'groovy'
apply plugin: 'ivy-publish'

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  compile localGroovy()
  compile gradleApi()
  compile 'org.antlr:antlr4:4.2.2'
}

antlr4 {
  
  extraArgs = ["-package", "org.fandianpf.testParser"]
 
  source = file('src/test/antlr4') 
  
  // make the Java compile task depend on the antlr4 task
  compileTestJava.dependsOn antlr4

  // add the generated source files to the list of java sources
  sourceSets.test.java.srcDirs += antlr4.output

  // add antlr4 to classpath
  configurations {
     testCompile.extendsFrom antlr4
  }
}

task testdoc(type: Groovydoc) {
  source = sourceSets.test.allGroovy
  destinationDir = file("${docsDir}/testdoc")
  classpath = sourceSets.test.runtimeClasspath
}

task docsArchive(type: Tar) {
  dependsOn testdoc
  dependsOn javadoc

  from(docsDir) {
    into "/"
  }

  from "LICENSE"
  
  extension = 'tgz'
  compression = Compression.GZIP
}

// add the project license to the jar artifacts inorder to conform to the
// requirments for the 3-clause BSD license.
jar {
  from "LICENSE"
}

publishing {  
  publications {
    repositories {
      ivy {
        // IF YOU ONLY want to "release" locally 
        // comment the next line AND uncomment the second line below.
        url fandianpfPublishRepo
        //url "$buildDir/ivyRepo/"
      }
    }

    ivyJava(IvyPublication) {
      descriptor.withXml {
        asNode().info[0].appendNode('description', 
        'Antlr4 RegressionTestRig tool')
      }

      organisation 'org.fandianpf'
      module 'antlr4-regressionTestRig'
      revision '0.1'
      
      from components.java
      
      artifact(docsArchive) {
        classifier =  'htmlDocs'
      }
    }    
  }
}

